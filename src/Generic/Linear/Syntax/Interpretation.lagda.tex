\begin{code}
{-# OPTIONS --safe --without-K #-}

open import Algebra.Skew
open import Level using (Level; 0ℓ; _⊔_)
open import Relation.Binary using (Rel)

module Generic.Linear.Syntax.Interpretation
  (Ty : Set) (rawSkewSemiring : RawSkewSemiring 0ℓ 0ℓ)
  where

  open RawSkewSemiring rawSkewSemiring renaming (Carrier to Ann; _≤_ to _⊴_)

  open import Algebra using (Op₂; Opₗ)
  open import Data.Product as ×
  open import Data.Unit
  open import Function
  open import Relation.Binary.PropositionalEquality
  open import Relation.Unary.Checked

  open import Data.LTree
  open import Data.LTree.Vector

  open import Generic.Linear.Operations rawSkewSemiring
  open import Generic.Linear.Syntax Ty Ann

  open import Relation.Unary.Bunched.Checked
  private
    open module Chk0 {s} = BunchedUnit (_⊴* 0* {s})
    open module Chk+ {s} =
      BunchedConjunction (λ (R P Q : Vector _ s) → R ⊴* P +* Q)
        hiding (_─✴_)
    open module Chk* {s} =
      BunchedScaling (λ (R : Vector _ s) r P → R ⊴* r *ₗ P)
    open module ChkD {s} =
      BunchedDuplicable (_⊴*_ {s}) 0* _+*_
  import Relation.Unary.Bunched as Syn
  private
    open module Syn⊴ {s} = Syn.BunchedOrder (_⊴*_ {s})
    open module Syn+ {s} =
      Syn.BunchedConjunction (λ (R P Q : Vector _ s) → R ⊴* P +* Q)
        using (_─✴_)

  private
    variable
      ℓ t u : Level

  infix 4 _⊴ᶜ_
  infixr 5 !!_
  infixr 8 _⇒ᵏᶜ_ _─✴ᶜ_
  infixr 9 _✴ᶜ_
  infixr 10 _·ᶜ_

  _⊴ᶜ_ : (PΓ QΔ : Ctx) → Set
  ctx {s} P Γ ⊴ᶜ ctx {t} Q Δ = Σ (s ≡ t) λ where
    refl → P ⊴* Q × Γ ≡ Δ

  pattern !!_ PQ = refl , PQ , refl

  _⇒ᵏᶜ_ : (T : Ctx → Set t) (U : Ctx → Set u) (RΓ : Ctx) → Set (t ⊔ u)
  (T ⇒ᵏᶜ U) (ctx R Γ) = ((λ P → T (ctx P Γ)) ⇒ᵏ (λ Q → U (ctx Q Γ))) R

  ℑᶜ : Ctx → Set ℓ
  ℑᶜ (ctx R Γ) = ℑ R

  _✴ᶜ_ : (T U : Ctx → Set ℓ) (RΓ : Ctx) → Set ℓ
  (T ✴ᶜ U) (ctx R Γ) = ((λ P → T (ctx P Γ)) ✴ (λ Q → U (ctx Q Γ))) R

  _─✴ᶜ_ : (T : Ctx → Set t) (U : Ctx → Set u) (RΓ : Ctx) → Set (t ⊔ u)
  (T ─✴ᶜ U) (ctx R Γ) = ((λ P → T (ctx P Γ)) ─✴ (λ Q → U (ctx Q Γ))) R

  _·ᶜ_ : (r : Ann) (T : Ctx → Set ℓ) (RΓ : Ctx) → Set ℓ
  (r ·ᶜ T) (ctx R Γ) = (r · (λ P → T (ctx P Γ))) R

  Dupᶜ : (T : Ctx → Set ℓ) (RΓ : Ctx) → Set ℓ
  Dupᶜ T (ctx R Γ) = Dup (λ P → T (ctx P Γ)) R

  module WithScope (⟦-,_⊢_⟧ : Ctx → Scoped ℓ) where
\end{code}

%<*semp>
\begin{code}
    ⟦_⟧p : Premises → Ctx → Set ℓ
    ⟦ ⟨ Γ `⊢ A ⟩ ⟧p = ⟦-, Γ ⊢ A ⟧
    ⟦ `⊤ ⟧p = U
    ⟦ `ℑ ⟧p = ℑᶜ
    ⟦ p `∧ q ⟧p = ⟦ p ⟧p ∩ ⟦ q ⟧p
    ⟦ p `✴ q ⟧p = ⟦ p ⟧p ✴ᶜ ⟦ q ⟧p
    ⟦ r `· p ⟧p = r ·ᶜ ⟦ p ⟧p
    ⟦ `□ p ⟧p = Dupᶜ ⟦ p ⟧p
\end{code}
%</semp>
\begin{code}
    -- ⟦ `□ p ⟧p = Dup (⟦ p ⟧p)
\end{code}

%<*semr>
\begin{code}
    ⟦_⟧r : Rule → Scoped ℓ
    ⟦ ps =⇒ A′ ⟧r A PΓ = A′ ≡ A × ⟦ ps ⟧p PΓ
\end{code}
%</semr>

%<*sems>
\begin{code}
    ⟦_⟧s : System → Scoped ℓ
    ⟦ L ▹ rs ⟧s A PΓ = Σ[ l ∈ L ] ⟦ rs l ⟧r A PΓ
\end{code}
%</sems>

\begin{code}
  ⟦_⟧p : Premises → (Ctx → Scoped ℓ) → (Ctx → Set ℓ)
  ⟦ ps ⟧p Sc = let module W = WithScope Sc in W.⟦ ps ⟧p

  ⟦_⟧r : Rule → (Ctx → Scoped ℓ) → Scoped ℓ
  ⟦ r ⟧r Sc = let module W = WithScope Sc in W.⟦ r ⟧r

  ⟦_⟧s : System → (Ctx → Scoped ℓ) → Scoped ℓ
  ⟦ s ⟧s Sc = let module W = WithScope Sc in W.⟦ s ⟧s
\end{code}
