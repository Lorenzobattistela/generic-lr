\begin{code}
{-# OPTIONS --safe --without-K --prop #-}

module Generic.Linear.Syntax (Ty Ann : Set) where

  open import Data.LTree
  open import Data.LTree.Vector
  open import Data.Product using (_×_; _,_)
  open import Level
  open import Function.Base using (_∘_; _⟨_⟩_)
  open import Relation.Unary

  open import Generic.Linear.Syntax.Core public

  infix 25 _++ᶜ_

  record SizedCtx (s : LTree) : Set where
    constructor sctx
    field
      R : Vector Ann s
      Γ : Vector Ty s

  record Ctx : Set where
    constructor ctx
    field
      {s} : LTree
      R : Vector Ann s
      Γ : Vector Ty s

    ctx→sctx : SizedCtx s
    ctx→sctx = sctx R Γ
  open Ctx public using (ctx→sctx)

  sctx→ctx : ∀ {s} → SizedCtx s → Ctx
  sctx→ctx (sctx P Γ) = ctx P Γ

  []ᶜ : Ctx
  []ᶜ = ctx [] []

  [_]ᶜ : Ann × Ty → Ctx
  [(r , A)]ᶜ = ctx [ r ] [ A ]

  [_·_]ᶜ : Ann → Ty → Ctx
  [ ρ · A ]ᶜ = ctx [ ρ ] [ A ]

  _++ᶜ_ : Ctx → Ctx → Ctx
  ctx P Γ ++ᶜ ctx Q Δ = ctx (P ++ Q) (Γ ++ Δ)

  leftᶜ : ∀ {s t} → SizedCtx (s <+> t) → Ctx
  leftᶜ (sctx P Γ) = ctx (P ∘ ↙) (Γ ∘ ↙)

  rightᶜ : ∀ {s t} → SizedCtx (s <+> t) → Ctx
  rightᶜ (sctx P Γ) = ctx (P ∘ ↘) (Γ ∘ ↘)
\end{code}

Premises to each rule form a tree.
At each leaf is a premise, which binds one Ctx's worth of new variables.
Annotations are shared out to the premises via separation logic
connectives:
\begin{verbatim}
\item separating conjunction (`I, _`*_) – e.g, ⊗-introduction
\item sharing conjunction (`⊤, _`∧_)    – e.g, &-introduction
\item scaling (_`·_)                    – e.g, !-introduction
\item the duplicable modality (`□)      – e.g, recursion rules
\end{verbatim}

\begin{code}
  module _ (fl : PremisesFlags) (open PremisesFlags fl) where

    infix 1 _=⇒_ _▹_
    infixr 2 _`✴_
    infixr 2 _`∧_
    infixr 3 _`·_
\end{code}

%<*Premises>
\begin{code}
    data Premises : Set where
      ⟨_`⊢_⟩ : (PΓ : Ctx) (A : Ty) → Premises
      `⊤ : {{_ : Has-⊤}} → Premises
      _`∧_ : {{_ : Has-∧}} (p q : Premises) → Premises
      `ℑ : {{_ : Has-ℑ}} → Premises
      _`✴_ : {{_ : Has-✴}} (p q : Premises) → Premises
      _`·_ : {{_ : Has-·}} (r : Ann) (p : Premises) → Premises
      `□ : {{_ : Has-□}} (p : Premises) → Premises
\end{code}
%</Premises>

%<*Rule>
\begin{code}
    record Rule : Set where
      constructor _=⇒_
      field
        premises : Premises
        conclusion : Ty
    open Rule public
\end{code}
%</Rule>

%<*System>
\begin{code}
    record System : Set₁ where
      constructor _▹_
      field
        Label : Set
        rules : (l : Label) → Rule
    open System public
\end{code}
%</System>

\begin{code}
  Scoped : ∀ ℓ → Set (suc ℓ)
  Scoped ℓ = Ctx → Ty → Set ℓ

  Scope : ∀ {ℓ} → Scoped ℓ → Ctx → Scoped ℓ
  Scope T QΔ PΓ A = T (PΓ ++ᶜ QΔ) A
\end{code}
